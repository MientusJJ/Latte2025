-- programs ------------------------------------------------

entrypoints Program ;

Prog.      Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClsDef.    TopDef ::= "class" Ident "{" [ClsFld] "}" ;

InhClsDef. TopDef ::= "class" Ident "extends" Ident "{" [ClsFld] "}" ;

separator ClsFld "" ;

separator nonempty TopDef "" ;

-- class block ---------------------------------------------

VarDef.    ClsFld ::= Type [Ident] ";" ;

MetDef.    ClsFld ::= Type Ident "(" [Arg] ")" Block ;

separator nonempty Ident "," ;

-- arguments -----------------------------------------------

Ar. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Blk.       Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Expr "=" Expr  ";" ;

Incr.      Stmt ::= Expr "++"  ";" ;

Decr.      Stmt ::= Expr "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForEach.   Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;
SExpParen. Stmt ::= "(" Expr ")" ";";

-- Types ---------------------------------------------------

Int.       SimpleType ::= "int" ;

Str.       SimpleType ::= "string" ;

Bool.      SimpleType ::= "boolean" ;

Void.      SimpleType ::= "void" ;

SimpleVarType. Type ::= SimpleType ;

SimpleArrType. Type ::= SimpleType "[]" ;

VarType.   Type ::= Ident ;

ArrType.   Type ::= Ident "[]" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr8 ::= Ident ;

EClsVar.   Expr8 ::= Expr8 "." Ident ;

EArrVar.   Expr8 ::= Expr8 "[" Expr "]" ;

ELitInt.   Expr8 ::= Integer ;

EString.   Expr8 ::= String ;

ELitTrue.  Expr8 ::= "true" ;

ELitFalse. Expr8 ::= "false" ;

ELitNull.  Expr8 ::= "(" Ident ")" "null" ;

EApp.      Expr8 ::= Ident "(" [Expr] ")" ;

EClsApp.   Expr8 ::= Expr8 "." Ident "(" [Expr] ")" ;

ENeg.      Expr7 ::= "-" Expr8 ;

ENot.      Expr7 ::= "!" Expr8 ;

EVarNew.   Expr6 ::= "new" Ident ;

EVarSimpleNew.  Expr6 ::= "new" SimpleType ;

EArrNew.   Expr6 ::= "new" Ident "[" Expr "]" ;

EArrSimpleNew.  Expr6 ::= "new" SimpleType "[" Expr "]" ;

EVarCast.  Expr5 ::= "(" Ident ")" Expr5 ;

EVarSimpleCast. Expr5 ::= "(" SimpleType ")" Expr5 ;

EArrCast.  Expr5 ::= "(" Ident "[]" ")" Expr5 ;

EArrSimpleCast. Expr5 ::= "(" SimpleType "[]" ")" Expr5 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 8 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;